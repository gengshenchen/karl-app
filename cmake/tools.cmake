function(find_deploy_qt_tool)
 if(NOT TARGET Qt6::Core AND NOT TARGET Qt5::Core)
        find_package(Qt6 QUIET COMPONENTS Core)
        if(NOT TARGET Qt6::Core)
            find_package(Qt5 QUIET COMPONENTS Core)
            if(NOT TARGET Qt5::Core)
                message(AUTHOR_WARNING "deploy_qt_runtime: You need to install the QtCore module first to be able to deploy the Qt libraries.")
                return()
            endif()
        endif()
    endif()
    # "QT_QMAKE_EXECUTABLE" is usually defined by QtCreator.
    if(NOT DEFINED QT_QMAKE_EXECUTABLE)
        get_target_property(QT_QMAKE_EXECUTABLE Qt::qmake IMPORTED_LOCATION)
    endif()
    if(NOT EXISTS "${QT_QMAKE_EXECUTABLE}")
        message(WARNING "deploy_qt_runtime: Can't locate the QMake executable.")
        return()
    endif()
    cmake_path(GET QT_QMAKE_EXECUTABLE PARENT_PATH __qt_bin_dir)
    find_program(__deploy_tool NAMES windeployqt macdeployqt HINTS "${__qt_bin_dir}")
    if(NOT EXISTS "${__deploy_tool}")
        message(WARNING "deploy_qt_runtime: Can't locate the deployqt tool.")
        return()
    endif()
    message(STATUS "__deploy_tool : ${__deploy_tool}")
    # 将变量传递到函数外部，通过参数传递
    set(${ARGV0} "${__deploy_tool}" PARENT_SCOPE)
endfunction()